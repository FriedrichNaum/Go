S -> FUNCTIONS
FUNCTIONS -> FUNCTION FUNCTIONS
FUNCTIONS -> FUNCTION

FUNCTION -> FUNC IDENTIFIER PARENTHESES_IN PARAM_LIST PARENTHESES_OUT BLOCK
FUNCTION -> FUNC IDENTIFIER PARENTHESES_IN PARENTHESES_OUT BLOCK

PARAM_LIST -> PARAM COMMA PARAM_LIST
PARAM_LIST -> PARAM
PARAM -> ID_LIST TYPE_T

ID_LIST -> IDENTIFIER COMMA ID_LIST
ID_LIST -> IDENTIFIER

BLOCK -> KEY_IN STATEMENT_LIST KEY_OUT
BLOCK -> KEY_IN KEY_OUT

STATEMENT_LIST -> STATEMENT STATEMENT_LIST
STATEMENT_LIST -> STATEMENT
STATEMENT -> IF_STATEMENT
STATEMENT -> FOR_STATEMENT
STATEMENT -> VAR_DECLARATION SEMICOLON
STATEMENT -> STRUCT_DECL
STATEMENT -> EXPRESSION SEMICOLON
STATEMENT -> RETURN EXPRESSION SEMICOLON
STATEMENT -> RETURN SEMICOLON
STATEMENT -> BLOCK

IF_STATEMENT -> IF EXPRESSION BLOCK
IF_STATEMENT -> IF EXPRESSION BLOCK ELSE_STATEMENT
ELSE_STATEMENT -> ELSE BLOCK
ELSE_STATEMENT -> ELSE IF_STATEMENT

FOR_STATEMENT -> FOR EXPRESSION BLOCK

VAR_DECLARATION -> IDENTIFIER _VAR_DECLARATION EXPRESSION
_VAR_DECLARATION -> COMMA IDENTIFIER _VAR_DECLARATION EXPRESSION COMMA
_VAR_DECLARATION -> OPERATOR_DECLARATION

STRUCT_DECL -> TYPE IDENTIFIER STRUCT KEY_IN PARAM_LIST KEY_OUT

EXPRESSION -> EXPR_LOGIC

EXPR_LOGIC -> EXPR_LOGIC AND EXPR_COMP
EXPR_LOGIC -> EXPR_LOGIC OR EXPR_COMP
EXPR_LOGIC -> EXPR_COMP

EXPR_COMP -> EXPR_ART SMALLER EXPR_ART
EXPR_COMP -> EXPR_ART BIGGER EXPR_ART
EXPR_COMP -> EXPR_ART LESS_OR_EQUAL EXPR_ART
EXPR_COMP -> EXPR_ART MORE_OR_EQUAL EXPR_ART
EXPR_COMP -> EXPR_ART EQUAL_COMPARISON EXPR_ART
EXPR_COMP -> EXPR_ART DIFFERENCE_COMPARISON EXPR_ART
EXPR_COMP -> EXPR_ART

EXPR_ART -> EXPR_ART SUM EXPR_MUL
EXPR_ART -> EXPR_ART DIFFERENCE EXPR_MUL
EXPR_ART -> EXPR_MUL

EXPR_MUL -> EXPR_MUL MULTIPLICATION PREFIX_EXPR
EXPR_MUL -> EXPR_MUL DIVISION PREFIX_EXPR
EXPR_MUL -> EXPR_MUL REST PREFIX_EXPR
EXPR_MUL -> PREFIX_EXPR

PREFIX_EXPR -> NOT ELEMENTO
PREFIX_EXPR -> ELEMENTO

ELEMENTO -> STRING_LITERAL
ELEMENTO -> BOOL
ELEMENTO -> NUMBER
ELEMENTO -> DIFFERENCE NUMBER
ELEMENTO -> IDENTIFIER
ELEMENTO -> FUNC_CALL
ELEMENTO -> IDENTIFIER MEMBER_ACCESS
ELEMENTO -> FUNC_CALL MEMBER_ACCESS
ELEMENTO -> PARENTHESES_IN EXPRESSION PARENTHESES_OUT
ELEMENTO -> ARRAY_LITERAL

NUMBER -> INT_LIT
NUMBER -> FLOAT_LIT

ARRAY_LITERAL -> BRACKET_IN EXPRESSION BRACKET_OUT TYPE_T KEY_IN EXPRESSION_LIST KEY_OUT
ARRAY_LITERAL -> BRACKET_IN EXPRESSION BRACKET_OUT TYPE_T KEY_IN KEY_OUT

MEMBER_ACCESS -> BRACKET_IN EXPR_LOGIC BRACKET_OUT _MEMBER_ACCESS
MEMBER_ACCESS -> PERIOD ID _MEMBER_ACCESS

_MEMBER_ACCESS -> MEMBER_ACCESS
_MEMBER_ACCESS -> ''

FUNC_CALL -> IDENTIFIER PARENTHESES_IN PARENTHESES_OUT
FUNC_CALL -> IDENTIFIER PARENTHESES_IN EXPRESSION_LIST PARENTHESES_OUT

TYPE_T -> IDENTIFIER
TYPE_T -> BRACKET_IN EXPRESSION BRACKET_OUT IDENTIFIER

EXPRESSION_LIST -> EXPRESSION COMMA EXPRESSION_LIST
EXPRESSION_LIST -> EXPRESSION
